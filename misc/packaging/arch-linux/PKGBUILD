# Maintainer: Will Reed <wreed@disroot.org>

pkgname=confidant
pkgver=0.4.0
pkgrel=1
pkgdesc="Dotfile manager; your configuration pal and confidant"
arch=('x86_64')
url="https://wreedb.github.io/confidant"
license=('GPL-3.0-or-later')
source=("https://codeberg.org/wreedb/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.source.tar.gz")
sha256sums=('0e121034dc933a50f38e73be04ba5207f7296ef3ed5f1ae2037011b19de17013')
provides=('confidant')
depends=(libucl)
makedepends=(
    meson
    scdoc
)

_srcdir="${pkgname}-${pkgver}"
_builddir=${_srcdir}/build

prepare() {
    cd ${_srcdir}
    meson setup build                  \
        --prefix=/usr                  \
        --buildtype release            \
        --force-fallback-for=lyra      \
        -D cpp_args="${CXXFLAGS}"      \
        -D cpp_link_args="${LDFLAGS}"
}

build() {
    meson compile -C ${_builddir}
}

package() {
    install -m 0755 ${_builddir}/confidant -Dt ${pkgdir}/usr/bin
    install -m 0644 ${_builddir}/confidant.1 -Dt ${pkgdir}/usr/share/man/man1
    install -m 0644 ${_builddir}/confidant.ucl.5 -Dt ${pkgdir}/usr/share/man/man5
    install -m 0644 ${_srcdir}/misc/completions/bash/confidant.bash -Dt ${pkgdir}/usr/share/bash-completion/completions
    install -m 0644 ${_srcdir}/misc/completions/fish/confidant.fish -Dt ${pkgdir}/usr/share/fish/vendor_completions.d
    install -m 0644 ${_srcdir}/misc/completions/zsh/_confidant -Dt ${pkgdir}/usr/share/zsh/site-functions
    install -m 0644 ${_srcdir}/LICENSE.md -Dt ${pkgdir}/usr/share/licenses/${pkgname}
}