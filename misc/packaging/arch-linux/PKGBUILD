# Maintainer: Will Reed <wreed@disroot.org>

pkgname=confidant
pkgver=0.3.0
pkgrel=1
pkgdesc="Dotfile manager; your configuration pal and confidant"
arch=('x86_64')
url="https://wreedb.github.io/confidant"
license=('GPL-3.0-or-later')
source=("https://codeberg.org/wreedb/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.source.tar.gz")
b2sums=('f3366840e045bea526e8e84497f3a57995fd9097d6b427efd9b74fa4ff4be7f8264e6e2428519d9464f4a6790c74372ba537686ca814cfd2761eb8945cd3718a')
provides=('confidant')
depends=(libucl cli11)
makedepends=(
    meson
    scdoc
)

_srcdir="${pkgname}-${pkgver}"
_builddir=${_srcdir}/build

prepare() {
    cd ${_srcdir}
    meson setup build                  \
        --prefix=/usr                  \
        --buildtype release            \
        -D cpp_args="${CXXFLAGS}"      \
        -D cpp_link_args="${LDFLAGS}"
}

build() {
    meson compile -C ${_builddir}
}

package() {
    meson install -C ${_builddir} --destdir ${pkgdir}
    install -dm 0755 ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm 0644 ${_srcdir}/LICENSE.md -t ${pkgdir}/usr/share/licenses/${pkgname}
}