# Maintainer: Will Reed <wreed@disroot.org>

pkgname=confidant
pkgver=0.1.1
pkgrel=1
pkgdesc="Dotfile manager; your configuration pal and confidant"
arch=('x86_64')
url="https://wreedb.github.io/confidant"
license=('GPL-3.0-or-later')
source=("https://codeberg.org/wreedb/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.source.tar.gz")
b2sums=('6e299bfeeedb4548c732d64b07fc430738c57a41c7bf2d766f9c9ed24ebd1ba540c66a2979062e7fc4bf9d67ff080bb7ee456d7948ebc3eec65fa382972ad9bc')
provides=('confidant')
depends=(libucl cli11)
makedepends=(
    meson
    scdoc
)

_srcdir="${pkgname}-${pkgver}.source"
_builddir=${_srcdir}/build

prepare() {
    cd ${_srcdir}
    meson setup build                  \
        --prefix=/usr                  \
        --buildtype release            \
        -D cpp_args="${CXXFLAGS}"      \
        -D cpp_link_args="${LDFLAGS}"  \
        -D man-pages=enabled
}

build() {
    meson compile -C ${_builddir}
}

package() {
    meson install -C ${_builddir} --destdir ${pkgdir}
    install -dm 0755 ${pkgdir}/usr/share/licenses/${pkgname}
    install -Dm 0644 ${_srcdir}/LICENSE.md -t ${pkgdir}/usr/share/licenses/${pkgname}
}